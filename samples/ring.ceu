/*
 * A token ring network example for WSNs.
 *
 * To compile it, follow the instructions at
 * http://www.ceu-lang.org/wiki/index.php?title=TinyOS
 *
 * Francisco Sant'Anna
 * http://www.lua.inf.puc-rio.br/~francisco/
 */

@include(radio.ceu)

C _Cnt = 0;
C do
    typedef struct {
        nx_uint16_t cnt;
    } Cnt;
end

@TOS_retry(1s, @RADIO_start);

event void retry;

par do
    loop do
        var _message_t* msg = await RADIO_RECEIVE;
        tmp _Cnt* data = _Radio_getPayload(msg, sizeof<_Cnt>);
        data:cnt = data:cnt + 1;
        _Leds_set(data:cnt);
        await 1s;
        _Radio_setDestination(msg, (_TOS_NODE_ID+1)%3);
        emit RADIO_SEND(msg);
    end
with
    loop do
        par/or do
            await 5s;
            par do
                loop do
                    emit retry;
                    await 10s;
                end
            with
                _Leds_set(0);
                loop do
                    _Leds_led0Toggle();
                    await 500ms;
                end
            end
        with
            await RADIO_RECEIVE;
        end
    end
with
    if _TOS_NODE_ID == 0 then
        loop do
            var _message_t msg;
            tmp _Cnt* data = _Radio_getPayload(&msg, sizeof<_Cnt>);
            data:cnt = 1;
            _Radio_setDestination(&msg, 1);
            _Radio_setPayloadLength(&msg, sizeof<_Cnt>);
            emit RADIO_SEND(&msg);
            await retry;
        end
    else
        await FOREVER;
    end
end

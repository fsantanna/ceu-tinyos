/*
 * A token ring network example for WSNs.
 *
 * To compile it, follow the instructions at
 * http://www.ceu-lang.org/wiki/index.php?title=TinyOS
 *
 * Francisco Sant'Anna
 * http://www.lua.inf.puc-rio.br/~francisco/
 */

#include "radio.ceu"

native _Cnt = 0;
native do
    typedef struct {
        nx_uint16_t cnt;
    } Cnt;
end

loop do
    var int err = _Radio_start();
    if err == _SUCCESS then
        err = await RADIO_STARTDONE;
        if err == _SUCCESS then
            break;
        end
    end
    await 1s;
end

event void retry;

par do
    var _message_t msg;
    loop do
        var _message_t** msg_ptr;
        var int len;    // TODO: not used
        (msg_ptr, len) = await RADIO_RECEIVE;
        var _Cnt* rcv = _Radio_getPayload(*msg_ptr, sizeof(_Cnt));
        _Leds_set(rcv:cnt);

        await 1s;

        var _Cnt* snd = _Radio_getPayload(&msg, sizeof(_Cnt));
        snd:cnt = rcv:cnt + 1;
        _Radio_setDestination(&msg, (_TOS_NODE_ID+1)%3);
        _Radio_setPayloadLength(&msg, sizeof(_Cnt));
        emit RADIO_SEND => (&msg,null);
    end
with
    loop do
        par/or do
            await 5s;
            par do
                loop do
                    emit retry;
                    await 10s;
                end
            with
                _Leds_set(0);
                loop do
                    _Leds_led0Toggle();
                    await 500ms;
                end
            end
        with
            await RADIO_RECEIVE;
        end
    end
with
    if _TOS_NODE_ID == 0 then
        loop do
            var _message_t msg;
            var _Cnt* data = _Radio_getPayload(&msg, sizeof(_Cnt));
            data:cnt = 1;
            _Radio_setDestination(&msg, _TOS_NODE_ID+1);
            _Radio_setPayloadLength(&msg, sizeof(_Cnt));
            emit RADIO_SEND => (&msg,null);
            await retry;
        end
    else
        await FOREVER;
    end
end
